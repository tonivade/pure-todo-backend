plugins {
  id 'java-library'
  id 'application'
  id 'de.undercouch.download' version '4.1.1'
  id 'org.mikeneck.graalvm-native-image' version '1.4.1'
}

archivesBaseName = 'todo-backend'
version = '0.1.0-SNAPSHOT'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

test {
  useJUnitPlatform()
}

javadoc.options {
  addStringOption('-release', '17')
}

dependencies {
  annotationProcessor 'com.github.tonivade:purejson:0.3-SNAPSHOT'
  implementation 'com.github.tonivade:purefun-monad:3.0'
  implementation 'com.github.tonivade:purefun-effect:3.0'
  implementation 'com.github.tonivade:purefun-transformer:3.0'
  runtimeOnly 'com.github.tonivade:purefun-instances:3.0'
  implementation 'com.github.tonivade:zeromock-server:0.16.0-SNAPSHOT'
  implementation 'com.github.tonivade:puredbc:0.4-SNAPSHOT'
  implementation 'com.github.tonivade:purecfg:0.4-SNAPSHOT'
  implementation 'com.github.tonivade:purejson:0.3-SNAPSHOT'
  implementation 'com.zaxxer:HikariCP:5.0.1'
  implementation 'org.flywaydb:flyway-core:5.2.4'
  runtimeOnly 'com.h2database:h2:2.0.206'
  runtimeOnly 'ch.qos.logback:logback-classic:1.2.10'
  testImplementation 'com.github.tonivade:zeromock-junit5:0.16.0-SNAPSHOT'
  testImplementation 'com.github.tonivade:zeromock-client:0.16.0-SNAPSHOT'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
  testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.8.2'
  testImplementation 'org.mockito:mockito-core:4.2.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:4.2.0'
}

application {
  mainClass = 'com.github.tonivade.todo.Application'
}

task fatJar(type: Jar) {
  manifest {
    attributes "Main-Class": "com.github.tonivade.todo.Application"
  }
  archiveClassifier = "all"
  duplicatesStrategy = "warn"
  from {
    configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

task downloadGraalvm(type: Download) {
  src "https://github.com/graalvm/graalvm-ce-dev-builds/releases/download/22.1.0-dev-20220127_2024/graalvm-ce-java17-linux-amd64-dev.tar.gz"
  dest new java.io.File(buildDir, "graalvm.tar.gz")
  onlyIfModified true
}

task extractGraalvm(dependsOn: downloadGraalvm, type: Exec) {
  workingDir "$buildDir"
  commandLine 'tar', 'zxvf', 'graalvm.tar.gz'
}

nativeImage {
  graalVmHome = "${buildDir}/graalvm-ce-java17-22.1.0-dev"
  mainClass = "com.github.tonivade.todo.Application"
  executableName = "todo-backend"
  arguments(
          '--verbose',
          '--report-unsupported-elements-at-runtime',
          '--initialize-at-build-time',
          '--initialize-at-run-time=org.h2.store.fs.niomem.FileNioMemData',
          '--no-fallback',
          '--allow-incomplete-classpath',
          '-H:IncludeResources=application.toml',
          '-H:ReflectionConfigurationFiles=src/native-image/reflection.json'
  )
}
