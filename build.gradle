plugins {
  id 'java-library'
  id 'application'
  id 'de.undercouch.download' version '5.5.0'
  id 'org.mikeneck.graalvm-native-image' version '1.4.1'
  id 'com.github.ben-manes.versions' version '0.48.0'
}

base {
  archivesBaseName = 'todo-backend'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(20)
  }
}

test {
  useJUnitPlatform()

  jvmArgs(['--enable-preview'])
}

javadoc.options {
  addStringOption('-release', '20')
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.compilerArgs << '--enable-preview'
  options.release = 20
}

compileTestJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.compilerArgs << '--enable-preview'
  options.release = 20
}

dependencies {
  annotationProcessor "com.github.tonivade:purejson:${purejson_version}"
  implementation "com.github.tonivade:purefun-monad:${purefun_version}"
  implementation "com.github.tonivade:purefun-effect:${purefun_version}"
  implementation "com.github.tonivade:purefun-transformer:${purefun_version}"
  runtimeOnly "com.github.tonivade:purefun-instances:${purefun_version}"
  implementation "com.github.tonivade:zeromock-server:${zeromock_version}"
  implementation "com.github.tonivade:puredbc:${puredbc_version}"
  implementation "com.github.tonivade:purecfg:${purecfg_version}"
  implementation "com.github.tonivade:purejson:${purejson_version}"
  implementation "com.zaxxer:HikariCP:${hikaricp_version}"
  runtimeOnly "com.h2database:h2:${h2_version}"
  runtimeOnly "ch.qos.logback:logback-classic:${logback_version}"

  testImplementation "com.github.tonivade:zeromock-junit5:${zeromock_version}"
  testImplementation "com.github.tonivade:zeromock-client:${zeromock_version}"
  testImplementation "com.github.tonivade:purecheck:${purecheck_version}"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
  testRuntimeOnly "org.junit.platform:junit-platform-runner:${junit_platform_version}"
  testImplementation "org.mockito:mockito-core:${mockito_version}"
  testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
  testImplementation "org.assertj:assertj-core:${assertj_version}"
}

application {
  mainClass = 'com.github.tonivade.todo.Application'
  applicationDefaultJvmArgs << '--enable-preview'
}

task fatJar(type: Jar) {
  manifest {
    attributes "Main-Class": "com.github.tonivade.todo.Application"
  }
  archiveClassifier = "all"
  duplicatesStrategy = "warn"
  from {
    configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

task downloadGraalvm(type: Download) {
  src "https://github.com/graalvm/graalvm-ce-dev-builds/releases/download/${graalvm_version}/graalvm-community-java20-linux-amd64-dev.tar.gz"
  dest new java.io.File(buildDir, "graalvm.tar.gz")
  onlyIfModified true
}

task extractGraalvm(dependsOn: downloadGraalvm, type: Exec) {
  workingDir "$buildDir"
  commandLine 'tar', 'zxvf', 'graalvm.tar.gz'
}

nativeImage {
  graalVmHome = "${buildDir}/graalvm-community-openjdk-20.0.1+9.1/"
  mainClass = "com.github.tonivade.todo.Application"
  executableName = "todo-backend"
  arguments(
          '--verbose',
          '--report-unsupported-elements-at-runtime',
          '--no-fallback',
          '--enable-preview',
          '--allow-incomplete-classpath',
          '--initialize-at-run-time=com.oracle.truffle.js.scriptengine.GraalJSEngineFactory',
          '-H:JNIConfigurationFiles=src/native-image/jni-config.json',
          '-H:ResourceConfigurationFiles=src/native-image/resource-config.json',
          '-H:ReflectionConfigurationFiles=src/native-image/reflect-config.json'
  )
}

task downloadUpx(type: Download) {
  src "https://github.com/upx/upx/releases/download/v${upx_version}/upx-${upx_version}-amd64_linux.tar.xz"
  dest new java.io.File(buildDir, "upx.tar.xz")
  onlyIfModified true
}

task extractUpx(dependsOn: downloadUpx, type: Exec) {
  workingDir "$buildDir"
  commandLine 'tar', '-xvf', 'upx.tar.xz'
}

task compressExecutable(dependsOn: extractUpx, type: Exec) {
  workingDir "$buildDir/upx-${upx_version}-amd64_linux"
  commandLine './upx', '--best', '../native-image/todo-backend'
}

installNativeImage {
  dependsOn extractGraalvm
}

nativeImage {
  finalizedBy compressExecutable
}
