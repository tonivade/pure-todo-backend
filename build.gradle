plugins {
  id 'java-library'
  id 'application'
  alias(libs.plugins.download)
  alias(libs.plugins.graalvmNativeImage)
  alias(libs.plugins.versions)
  alias(libs.plugins.errorprone)
}

def system = org.gradle.internal.os.OperatingSystem.current()
def arch = System.getProperty("os.arch")
if (system.isLinux() && arch == "amd64") {
  project.ext.upxArch = "amd64_linux"
  project.ext.upxExt = "tar.xz"
} else if (system.isLinux() && arch == "aarch64") {
  project.ext.upxArch = "arm64_linux"
  project.ext.upxExt = "tar.xz"
}

group = 'com.github.tonivade'

base {
  archivesName = 'todo-backend'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://central.sonatype.com/repository/maven-snapshots/'
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

test {
  useJUnitPlatform()
}

javadoc.options {
  addStringOption('-release', '25')
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 25
}

compileTestJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 25
}
  
tasks.withType(JavaCompile) {
  if (!name.toLowerCase().contains("test")) {
    options.errorprone {
      check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
      option("NullAway:AnnotatedPackages", "com.github.tonivade.todo")
      option("NullAway:SuggestSuppressions", "true")
    }
  } else {
    options.errorprone.enabled = false
  }
}

dependencies {
  errorprone libs.nullaway
  errorprone libs.errorprone
  annotationProcessor libs.purejson
  implementation libs.purefunMonad
  implementation libs.purefunEffect
  implementation libs.purefunTransformer
  runtimeOnly libs.purefunInstances
  implementation libs.zeromockServer
  implementation libs.puredbc
  implementation libs.purecfg
  implementation libs.purejson
  implementation libs.hikaricp
  runtimeOnly libs.h2
  runtimeOnly libs.logbackClassic

  testImplementation platform(libs.junitBom)
  testImplementation libs.zeromockJunit5
  testImplementation libs.zeromockClient
  testImplementation libs.purecheck
  testImplementation libs.junitJupiter
  testRuntimeOnly libs.junitLauncher
  testImplementation libs.mockitoCore
  testImplementation libs.mockitoJunitJupiter
  testImplementation libs.assertjCore
}

application {
  mainClass = 'com.github.tonivade.todo.App'
}

task fatJar(type: Jar) {
  manifest {
    attributes "Main-Class": "com.github.tonivade.todo.App"
  }
  archiveClassifier = "all"
  duplicatesStrategy = "warn"
  from {
    configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

graalvmNative {
  toolchainDetection = true
  binaries {
    main {
      imageName = "todo-backend"
      mainClass = "com.github.tonivade.todo.App"
      sharedLibrary = false
      fallback = false
      buildArgs.add("-O2")
      buildArgs.add('--verbose')
      buildArgs.add('--report-unsupported-elements-at-runtime')
      buildArgs.add('--no-fallback')
      buildArgs.add('--allow-incomplete-classpath')
      buildArgs.add('-J-Xms2G')
      buildArgs.add('-J-Xmx2G')
    }
    test {
      buildArgs.add("-O0")
    }
  }
}

task downloadUpx(type: Download) {
  src "https://github.com/upx/upx/releases/download/v${upx_version}/upx-${upx_version}-${upxArch}.${upxExt}"
  dest new java.io.File(buildDir, "upx.tar.xz")
  onlyIfModified true
}

task extractUpx(dependsOn: downloadUpx, type: Exec) {
  workingDir "$buildDir"
  commandLine 'bash', '-c', 'mkdir -p upx && tar -xvf upx.tar.xz -C upx --strip-components 1'
}

task compressExecutable(dependsOn: extractUpx, type: Exec) {
  workingDir "$buildDir/upx"
  commandLine 'bash', '-c', './upx --best ../native/nativeCompile/todo-backend'
}

nativeCompile {
  finalizedBy compressExecutable
}
