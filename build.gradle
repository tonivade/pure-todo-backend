apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'com.diffplug.eclipse.apt'
apply plugin: 'org.mikeneck.graalvm-native-image'
apply plugin: 'de.undercouch.download'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.diffplug.gradle:goomph:3.25.0'
    classpath "gradle.plugin.org.mikeneck:graalvm-native-image-plugin:1.3.0"
    classpath 'de.undercouch:gradle-download-task:4.1.1'
  }
}

archivesBaseName = 'todo-backend'
version = '0.1.0-SNAPSHOT'

sourceCompatibility = 16
targetCompatibility = 16

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

tasks.withType(JavaCompile) {
  options.fork = true
}

test {
  useJUnitPlatform()
}

javadoc.options {
  addStringOption('-release', '16')
}

dependencies {
  annotationProcessor 'com.github.tonivade:purejson:0.2-SNAPSHOT'
  implementation 'com.github.tonivade:purefun-monad:2.3-SNAPSHOT'
  implementation 'com.github.tonivade:purefun-effect:2.3-SNAPSHOT'
  runtimeOnly 'com.github.tonivade:purefun-instances:2.3-SNAPSHOT'
  implementation 'com.github.tonivade:zeromock-server:0.15.0-SNAPSHOT'
  implementation 'com.github.tonivade:puredbc:0.3-SNAPSHOT'
  implementation 'com.github.tonivade:purecfg:0.3-SNAPSHOT'
  implementation 'com.github.tonivade:purejson:0.2-SNAPSHOT'
  implementation 'com.zaxxer:HikariCP:3.4.2'
  implementation 'org.flywaydb:flyway-core:5.2.4'
  implementation 'com.h2database:h2:1.4.200'
  testImplementation 'com.github.tonivade:zeromock-junit5:0.15.0-SNAPSHOT'
  testImplementation 'com.github.tonivade:zeromock-client:0.15.0-SNAPSHOT'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
  testImplementation 'org.junit.platform:junit-platform-runner:1.7.0'
  testImplementation 'org.mockito:mockito-core:3.7.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:3.7.0'
}

application {
  mainClassName = 'com.github.tonivade.todo.Application'
}

task fatJar(type: Jar) {
  manifest {
    attributes "Main-Class": "com.github.tonivade.todo.Application"
  }
  archiveClassifier = "all"
  duplicatesStrategy = "warn"
  from {
    configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

task downloadGraalvm(type: Download) {
  src "https://github.com/graalvm/graalvm-ce-dev-builds/releases/download/21.2.0-dev-20210427_1942/graalvm-ce-java16-linux-amd64-dev.tar.gz"
  dest new java.io.File(buildDir, "graalvm.tar.gz")
  onlyIfModified true
}

task extractGraalvm(dependsOn: downloadGraalvm, type: Exec) {
  workingDir "$buildDir"
  commandLine 'tar', 'zxvf', 'graalvm.tar.gz'
}

nativeImage {
  graalVmHome = "${buildDir}/graalvm-ce-java16-21.2.0-dev"
  mainClass = "com.github.tonivade.todo.Application"
  executableName = "todo-backend"
  arguments(
          '--verbose',
          '--report-unsupported-elements-at-runtime',
          '--initialize-at-build-time',
          '--no-fallback',
          '--allow-incomplete-classpath',
          '--initialize-at-run-time=org.h2.store.fs.FileNioMemData',
          '-H:IncludeResources=application.toml',
          '-H:ReflectionConfigurationFiles=src/native-image/reflection.json'
  )
}